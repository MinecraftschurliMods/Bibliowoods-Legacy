plugins {
    id 'net.neoforged.moddev' version '1.0.20'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.minecraftschurli.at/maven-public' }
    maven { url 'https://maven.blamejared.com/' }
    maven { url 'https://cursemaven.com' }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    // Uncomment next line for local testing
    implementation files("libs/bibliocraft-${mc_version}-${bc_version}.jar")

    //compileOnly "com.github.minecraftschurlimods:bibliocraft:${mc_version}-${bc_version}:api"
    //runtimeOnly "com.github.minecraftschurlimods:bibliocraft:${mc_version}-${bc_version}"
    compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"

    // Aether
    implementation "curse.maven:aether-255308:6352383"
    implementation "curse.maven:owo-lib-532610:6314117"
    // Allthemodium
    implementation "curse.maven:allthemodium-364466:6135138"
    // Ars Nouveau
    implementation "curse.maven:ars-nouveau-401955:6333245"
    implementation "curse.maven:curios-continuation-1037991:5888964"
    implementation "curse.maven:geckolib-388172:6304958"
    // Biomes O' Plenty
    implementation "curse.maven:biomes-o-plenty-220318:6152943"
    implementation "curse.maven:glitchcore-955399:5660740"
    implementation "curse.maven:terrablender-neoforge-940057:6054947"
    // Biomes We've Gone
    implementation "curse.maven:oh-the-biomes-weve-gone-1070751:6298837"
    implementation "curse.maven:corgilib-693313:6002859"
    implementation "curse.maven:oh-the-trees-youll-grow-962544:6314894"
    // Cobblemon
    implementation "curse.maven:cobblemon-687131:6125026"
    implementation "curse.maven:kotlin-for-forge-351264:5978393"
    // Deeper and Darker
    implementation "curse.maven:deeperdarker-659011:5908863"
    // Forbidden Arcanus
    implementation "curse.maven:forbidden-arcanus-309858:6331424"
    implementation "curse.maven:valhelsia-core-416935:6296775"
    // GregTech
    implementation "curse.maven:gregtechceu-modern-890405:5812613"
    // Integrated Dynamics
    implementation "curse.maven:integrated-dynamics-236307:6331508"
    implementation "curse.maven:common-capabilities-247007:6332022"
    implementation "curse.maven:cyclops-core-232758:6340387"
    // Occultism
    implementation "curse.maven:occultism-361026:6355135"
    implementation "curse.maven:modonomicon-538392:6338537"
    implementation "curse.maven:smartbrainlib-661293:6101242"
    // Regions Unexplored
    implementation "curse.maven:regions-unexplored-659110:6273893"
    // Silent Gear
    implementation "curse.maven:silent-gear-297039:6333121"
    implementation "curse.maven:silent-lib-242998:6285150"
    // Twilight Forest
    implementation "curse.maven:the-twilight-forest-227639:6087105"
    // Undergarden
    implementation "curse.maven:the-undergarden-379849:5769852"
}

neoForge {
    version = project.neo_version

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    runs {
        configureEach {
            systemProperty 'forge.logging.console.level', 'debug'
        }
        client {
            client()
        }
        data {
            data()
            mods = [neoForge.mods."${mod_id}"]
            programArguments.addAll '--mod', "${mod_id}", '--all',
                    '--output',       file('src/generated/resources/').getAbsolutePath(),
                    '--existing',     file('src/main/resources/').getAbsolutePath(),
                    '--existing-mod', 'bibliocraft',
                    '--existing-mod', 'aether',
                    '--existing-mod', 'allthemodium',
                    '--existing-mod', 'ars_nouveau',
                    '--existing-mod', 'biomesoplenty',
                    '--existing-mod', 'biomeswevegone',
                    '--existing-mod', 'cobblemon',
                    '--existing-mod', 'deeperdarker',
                    '--existing-mod', 'forbidden_arcanus',
                    '--existing-mod', 'gtceu',
                    '--existing-mod', 'integrateddynamics',
                    '--existing-mod', 'occultism',
                    '--existing-mod', 'regions_unexplored',
                    '--existing-mod', 'silentgear',
                    '--existing-mod', 'twilightforest',
                    '--existing-mod', 'undergarden'
        }
        server {
            server()
            programArgument '--nogui'
        }
    }
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
            mc_version       : mc_version,
            mc_version_range : mc_version_range,
            neo_version      : neo_version,
            neo_version_range: neo_version_range,
            bc_version       : bc_version,
            bc_version_range : bc_version_range,
            loader_version   : loader_version,
            mod_id           : mod_id,
            mod_version      : mod_version,
            mod_name         : mod_name,
            mod_authors      : mod_authors,
            mod_credits      : mod_credits,
            mod_logo         : mod_logo,
            mod_description  : mod_description,
            mod_license      : mod_license,
    ]
    inputs.properties replaceProperties
    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

jar {
    archiveBaseName = mod_id
    archiveVersion = "${mc_version}-${mod_version}"
    processResources.exclude('**/.cache/**')
}
