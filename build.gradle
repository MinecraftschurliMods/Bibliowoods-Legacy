plugins {
    id 'net.neoforged.moddev' version '2.0.88'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.minecraftschurli.at/maven-public' }
    maven { url 'https://maven.blamejared.com/' }
    maven { url 'https://mvn.devos.one/snapshots' }
    maven { url 'https://cursemaven.com' }
    maven { url 'https://api.modrinth.com/maven' }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    // Uncomment next line for local testing
    //implementation files("libs/bibliocraft-${mc_version}-${bc_version}.jar")

    compileOnly "com.github.minecraftschurlimods:bibliocraft:${mc_version}-${bc_version}:api"
    runtimeOnly ("com.github.minecraftschurlimods:bibliocraft:${mc_version}-${bc_version}") {
        exclude module: "gallery-1173553"
        exclude module: "blueprint-382216"
        exclude module: "buzzier-bees-355458"
    }
    compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"

    // At this point, we need some performance boosters.
    implementation "curse.maven:ferritecore-429235:5850121"
    implementation "curse.maven:modernfix-790626:6766126"

    // Aether
    implementation "curse.maven:aether-255308:6389857"
    implementation "curse.maven:owo-lib-532610:6785734"
    // Allthemodium
    implementation "curse.maven:allthemodium-364466:6638078"
    // Ars Nouveau
    implementation "curse.maven:ars-nouveau-401955:6770346"
    implementation "curse.maven:curios-309927:6529130"
    implementation "curse.maven:geckolib-388172:6659026"
    // Butchercraft
    implementation "curse.maven:butchercraft-265715:6714774"
    // Cobblemon
    implementation "curse.maven:cobblemon-687131:6125026"
    implementation "curse.maven:kotlin-for-forge-351264:6706911"
    // Create: Garnished
    implementation "curse.maven:garnished-923277:6551436"
    implementation "curse.maven:create-328085:6323264"
    compileOnly "com.tterrag.registrate:Registrate:MC1.21-1.3.0+66"
    // Deep Aether
    implementation "curse.maven:deep-aether-852465:6512691"
    // Deeper and Darker
    implementation "curse.maven:deeperdarker-659011:6463247"
    // Eternal Starlight
    implementation "curse.maven:eternal-starlight-1080592:6797801"
    // Expanded Delight
    implementation "curse.maven:expanded-delight-620770:6785113"
    implementation "curse.maven:farmers-delight-398521:6597295"
    // Extra Delight
    implementation "curse.maven:extradelight-842106:6066996"
    // Forbidden Arcanus
    implementation "curse.maven:forbidden-arcanus-309858:6651314"
    implementation "curse.maven:valhelsia-core-416935:6296775"
    // GregTech
    implementation "curse.maven:gregtechceu-modern-890405:6792853"
    // Hexerei
    implementation "maven.modrinth:hexerei:0.5.0.3"
    // Integrated Dynamics
    implementation "curse.maven:integrated-dynamics-236307:6784675"
    implementation "curse.maven:common-capabilities-247007:6783311"
    implementation "curse.maven:cyclops-core-232758:6783344"
    // Just Another Witchery Remake
    implementation "curse.maven:just-another-witchery-remake-1125946:6690260"
    implementation "curse.maven:architectury-api-419699:5786327"
    // Mirthdew Encore
    implementation "maven.modrinth:mirthdew-encore:0.2.0.3.neoforge"
    // My Nether's Delight
    implementation "curse.maven:my-nethers-delight-1003673:6460371"
    // Occultism
    implementation "curse.maven:occultism-361026:6700987"
    implementation "curse.maven:modonomicon-538392:6741276"
    implementation "curse.maven:smartbrainlib-661293:6762621"
    // Roots
    implementation "curse.maven:roots-246183:6777408"
    // Silent Gear
    implementation "curse.maven:silent-gear-297039:6761558"
    implementation "curse.maven:silent-lib-242998:6565251"
    // Thaumon
    implementation "curse.maven:thaumon-926511:5492714"
    // Tropicraft
    implementation "curse.maven:tropicraft-254794:6600109"
    // Twilight Forest
    implementation "curse.maven:the-twilight-forest-227639:6472889"
    // Undergarden
    implementation "curse.maven:the-undergarden-379849:6687578"
    // Vampirism
    implementation "curse.maven:vampirism-become-a-vampire-233029:6544569"
    // Wandering Wizardry
    implementation "curse.maven:wandering-wizardry-1106867:6265004"
    // Werewolves
    implementation "curse.maven:werewolves-become-a-beast-417851:6787229"
}

neoForge {
    version = project.neo_version

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.DEBUG
            // We ran out of RAM before.
            jvmArgument('-Xmx6G')
        }
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', "${mod_id}", '--all',
                    '--output',       file('src/generated/resources/').getAbsolutePath(),
                    '--existing',     file('src/main/resources/').getAbsolutePath(),
                    '--existing-mod', 'bibliocraft',
                    '--existing-mod', 'aether',
                    '--existing-mod', 'allthemodium',
                    '--existing-mod', 'ars_nouveau',
                    '--existing-mod', 'butchercraft',
                    '--existing-mod', 'cobblemon',
                    '--existing-mod', 'deep_aether',
                    '--existing-mod', 'deeperdarker',
                    '--existing-mod', 'eternal_starlight',
                    '--existing-mod', 'expandeddelight',
                    '--existing-mod', 'extradelight',
                    '--existing-mod', 'forbidden_arcanus',
                    '--existing-mod', 'garnished',
                    '--existing-mod', 'gtceu',
                    '--existing-mod', 'hexerei',
                    '--existing-mod', 'integrateddynamics',
                    '--existing-mod', 'mirthdew_encore',
                    '--existing-mod', 'mynethersdelight',
                    '--existing-mod', 'occultism',
                    '--existing-mod', 'roots',
                    '--existing-mod', 'silentgear',
                    '--existing-mod', 'thaumon',
                    '--existing-mod', 'tropicraft',
                    '--existing-mod', 'twilightforest',
                    '--existing-mod', 'undergarden',
                    '--existing-mod', 'vampirism',
                    '--existing-mod', 'werewolves',
                    '--existing-mod', 'witchery',
                    '--existing-mod', 'wwizardry'
        }
        server {
            server()
            programArgument '--nogui'
        }
    }
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
            mc_version       : mc_version,
            mc_version_range : mc_version_range,
            neo_version      : neo_version,
            neo_version_range: neo_version_range,
            bc_version       : bc_version,
            bc_version_range : bc_version_range,
            loader_version   : loader_version,
            mod_id           : mod_id,
            mod_version      : mod_version,
            mod_name         : mod_name,
            mod_authors      : mod_authors,
            mod_credits      : mod_credits,
            mod_logo         : mod_logo,
            mod_description  : mod_description,
            mod_license      : mod_license,
    ]
    inputs.properties replaceProperties
    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

jar {
    archiveBaseName = mod_id
    archiveVersion = "${mc_version}-${mod_version}"
    processResources.exclude('**/.cache/**')
}
